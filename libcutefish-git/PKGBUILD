pkgname=libcutefish-git
_pkgname=libcutefish
pkgver=0.7.r0.g7a1f2fa
pkgrel=1
pkgdesc="System library for Cutefish applications"
arch=('x86_64')
url="https://github.com/cutefishos/libcutefish"
license=('GPL3')
depends=('libc6' 'libcanberra0' 'libgcc-s1' 'libkf5bluezqt6' 'libkf5coreaddons5' 'libkf5networkmanagerqt6' 'libkf5screen-bin' 'libkf5screen7' 'libpulse-mainloop-glib0'  'libpulse0' 'libqt5core5a' 'libqt5dbus5' 'libqt5gui5 | libqt5gui5-gles' 'libqt5network5' 'libqt5qml5' 'libqt5quick5 | libqt5quick5-gles' 'libqt5sensors5' 'libqt5widgets5' 'libstdc++6')
makedepends=('qtbase5-dev' 'qtquickcontrols2-5-dev' 'modemmanager-qt-dev' 'git' 'qttools5-dev' 'qttools5-dev-tools')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git+$url.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $_pkgname

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd $_pkgname
  DESTDIR="$pkgdir" ninja install
}
